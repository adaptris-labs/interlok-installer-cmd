import org.apache.tools.ant.filters.*

plugins {
  id "application"
  id "maven-publish"
}

ext {
  releaseVersion = project.findProperty("releaseVersion") ?: "4.4.0-RELEASE"
  nexusBaseUrl = project.findProperty("nexusBaseUrl") ?: "https://nexus.adaptris.net/nexus"
  mavenPublishUrl = project.findProperty("mavenPublishUrl") ?: nexusBaseUrl + "/content/repositories/snapshots"
  repoUsername = project.findProperty("repoUsername") ?: "set in gradle.properties"
  repoPassword = project.findProperty("repoPassword") ?: "set in gradle.properties"

  componentName = "Interlok Installer CMD"
  componentDesc = "Interlok command line installer"
  organizationName = "Adaptris Ltd"
  organizationUrl = "http://interlok.adaptris.net"
}

repositories {
  mavenCentral()
  maven { url "$nexusBaseUrl/content/groups/public" }
  maven { url "$nexusBaseUrl/content/groups/interlok" }
}

dependencies {
}

group   = "com.adaptris"
version = releaseVersion

jar.enabled = false
startScripts.enabled = false

processResources {
  filesMatching("**/*.properties") {
    filter ReplaceTokens, tokens: [
	    "release.version": project.property("releaseVersion")
	  ]
  }
}

distTar {
  compression = Compression.GZIP
  archiveExtension = "tar.gz"
}

distributions {
  main {
    contents {
      from 'build/resources/main'
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact distZip
      artifact distTar
      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", componentDesc)
        def properties = asNode().appendNode("properties")
        properties.appendNode("target", "4.0.0+")
        properties.appendNode("license", "false")
        properties.appendNode("readme", "https://raw.githubusercontent.com/adaptris/interlok-installer-cmd/develop/README.md")
        properties.appendNode("repository", "https://github.com/adaptris/interlok-installer-cmd")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}
